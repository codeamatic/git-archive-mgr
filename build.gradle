ext {
    javaVersion = '1.7'
    slf4jVersion = '1.7.12'
    log4jVersion = '2.5'
}

wrapper.gradleVersion = '2.4'

buildscript {
    ext.springRepo = 'http://repo.spring.io/libs-release'

    repositories {
        maven { url springRepo }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.1.12.RELEASE"
        classpath 'org.cloudfoundry:cf-gradle-plugin:1.1.1' // see deploy.gradle
    }
}

configure(allprojects) {
    apply plugin: 'java'
    apply plugin: 'idea'

    targetCompatibility = javaVersion
    sourceCompatibility = javaVersion

    repositories {
        mavenCentral()
        maven { url springRepo }
    }

    dependencies {
        compile "org.slf4j:slf4j-api:$slf4jVersion"
        compile "org.apache.logging.log4j:log4j-api:$log4jVersion"
        compile "org.apache.logging.log4j:log4j-core:$log4jVersion"
        compile "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
        runtime "org.apache.logging.log4j:log4j-web:$log4jVersion"
    }

    configurations {
        // replaced with jcl-over-slf4j
        all*.exclude group: 'commons-logging', module: 'commons-logging'
        // replaced with log4j-over-slf4j
        all*.exclude group: 'log4j', module: 'log4j'
        // exclude logback classic
        all*.exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
}